'''
#Software
GATK version 4.0.5.2
Platypus version 0.8.1
samtools version 1.7, htslib version 1.7.2
freebayes v1.2.0
vcftools
Baysic v1.0
bedtools v2.26.0
'''
SAMPLES, = glob_wildcards("../temp/alignments/africa_cerana/{sample}.bam")

REF = "../ref/GCF_000002195.4_Amel_4.5_genomic.fna"
GFF = "../ref/GCF_000002195.4_Amel_4.5_genomic.gff"
THREADS = 20
PLATYPUS = "/home/warnerm/local_install/Platypus_0.8.1/Platypus.py"
CALLER=["samtools", "platypus"]

rule all:
	input: "../var/snpEff.txt"

rule makeBED:
	input: GFF
	output: "../ref/exons.bed"
	shell: """grep "exon" {GFF} | gff2bed > {output}"""

rule makeDict:
	input: REF
	output: "../ref/GCF_000002195.4_Amel_4.5_genomic.dict"
	shell: "java -jar {PICARD} CreateSequenceDictionary R={input} O={output}"


#'-=' means report genome qualities, the params makes a list of input files, min-alternate-count sets minimum number of chromosomes sampled to consider an allele
#Using target= exons.bed because we only want to call variants for exons
#rule freeBayes:
#	input: expand("../temp/alignments/africa_cerana/sorted/{sample}.bam",sample=SAMPLES),bed = "../ref/exons.bed"
#	output: protected("../var/freebayes.vcf")
#	params: files = "-b " + " -b ".join(expand("../temp/alignments/africa_cerana/sorted/{sample}.bam",sample=SAMPLES))
#	shell: "freebayes -= {params.files} -v {output} -f {REF} --min-alternate-count 4 --use-best-n-alleles 3 --targets ../ref/exons/bed"

#rule GATK:
#	input: expand("../temp/alignments/africa_cerana/sorted/{sample}.bam",sample=SAMPLES),bed = "../ref/exons.bed",dict="../ref/GCF_000002195.4_Amel_4.5_genomic.dict"
#	output: protected("../var/GATK.vcf")
#	params: files = "-I " + " -I ".join(expand("../temp/alignments/africa_cerana/sorted/{sample}.bam",sample=SAMPLES))
#	shell: """export PATH=~/local_install/jdk1.8.0_05/bin:$PATH; gatk --java-options "-Xmx30g" HaplotypeCaller \
#				-R {REF} \
#				{params.files} \
#				-O {output} \
#				-L ../ref/exons.bed \
#				--heterozygosity 0.002 \
#				--mbq 20 \
#				--max-alternate-alleles 4"""

rule samtools:
	input: BAMs = expand("../temp/alignments/africa_cerana/{sample}.bam",sample=SAMPLES),bed = "../ref/exons.bed"
	output: protected("../var/samtools.vcf")
	shell: "samtools mpileup -l {input[bed]} -ugf {REF} {input[BAMs]} | bcftools call -vc - | vcfutils.pl varFilter -D 500 > {output}"


rule platypus:
	input: expand("../temp/alignments/africa_cerana/{sample}.bam",sample=SAMPLES),bed = "../ref/exons.bed"
	output: protected("../var/platypus.vcf")
	params: files = ",".join(expand("../temp/alignments/africa_cerana/{sample}.bam",sample=SAMPLES))
	shell: "python2.7 {PLATYPUS} callVariants --nCPU={THREADS} --refFile={REF} --bamFiles={params.files} --output={output} --maxReads=25000000 --regions=../ref/exons.bed"

#remove insertions and deletions
rule rmIndel:
	input: "../var/{VCFcaller}.vcf"
	output: "../var/{VCFcaller}.snp.recode.vcf"
	shell: "vcftools --vcf {input} --remove-indels --out ../var/{wildcards.VCFcaller}.snp --recode --recode-INFO-all"

rule allelicPrimitives:
	input: "../var/{VCFcaller}.snp.recode.vcf"
	output: "../var/{VCFcaller}.snp.primatives.vcf"
	shell: "cat {input} | vcfallelicprimitives -kg > {output}"

rule vcfSort:
	input: "../var/{VCFcaller}.snp.primatives.vcf"
	output: "../var/{VCFcaller}.snp.primatives.sorted.vcf"
	shell: "vcf-sort {input} > {output}"

# generate consensus SNP calls
rule BAYSIC: 	
	input: expand("../var/{VCFcaller}.snp.primatives.sorted.vcf", VCFcaller=CALLER)
	output: "../var/consensus.vcf.pos"
	run: 
		infiles = "".join([" --vcf " + i for i in input])
		shell("perl baysic.pl --statsOutFile ../var/combined.stats --pvalCutoff 0.8 {} --countsOutFile ../var/combined.cts --vcfOutFile ../var/consensus.vcf".format(infiles))

# select bi-allelic consensus sites
rule consensusFilter:
     input: "../var/consensus.vcf","../var/consensus.vcf.pos"
     output: "../var/final.recode.vcf"
     shell: "vcftools --vcf {input[0]} --positions {input[1]} --max-alleles 2 --remove-indels --max-missing 0.9 --recode --mac 1 --out  ../var/final"

# estimate SNP effects
rule snpEff:
	input: rules.consensusFilter.output
	output: "../var/snpEff.txt"
	shell: "java -Xmx7g -jar /home/warnerm/local_install/snpEff/snpEff.jar -no-utr -no-upstream -no-intron -no-intergenic -no-downstream a_mellifera {input} >  {output}"

rule getCDS:
	input: GFF, REF
	output: "../ref/cds.fa"
	shell: "gffread {input[0]} -g {input[1]} -x {output}"

rule filterLongest:
	input: rules.getCDS.output
	output: "../ref/longest.fa"
	shell: "python filter_longest.py {input} > {output}"


